1 GOTO9503
189 REM ***** MENU PRINCIPAL, AL LLEGAR AQUI SE RECALCULAN PRECIOS Y DESDE AQUI VAMOS A VIAJAR, COMPRAVENTA E INVENTARIO *****
190 CLS:DAY=DAY+1:PROFO=0:FORC=0TO7:C(C)=0:NEXTC
200 LOCATE 1,2:PRINT "V";VIDA
201 LOCATE 7,2:PRINT "D";DAY
202 LOCATE 11,2:PRINT USING"$ #########";DINERO
203 LOCATE 24,2:PRINT USING"% ######";DEUDA
209 X=12:Y=4:T$=B$(BARRIO):GOSUB 9100
210 LOCATE 6,7: T$="ANFETAMINA":P=45:GOSUB 20000:E(0)=C: R=D(BARRIO,0):GOSUB 20005:P(0)=M:PRINT T$;M
220 LOCATE 6,8: T$="HACHIS    ":P=35:GOSUB 20000:E(1)=C: R=D(BARRIO,1):GOSUB 20005:P(1)=M:PRINT T$;M
230 LOCATE 6,9: T$="MARIHUANA ":P=25:GOSUB 20000:E(2)=C: R=D(BARRIO,2):GOSUB 20005:P(2)=M:PRINT T$;M
240 LOCATE 6,10: T$="COCAINA   ":P=20:GOSUB 20000:E(3)=C: R=D(BARRIO,3):GOSUB 20005:P(3)=M:PRINT T$;M
250 LOCATE 6,11: T$="MDMA      ":P=18:GOSUB 20000:E(4)=C: R=D(BARRIO,4):GOSUB 20005:P(4)=M:PRINT T$;M
260 LOCATE 6,12: T$="KRIPTONITA":P=16:GOSUB 20000:E(5)=C: R=D(BARRIO,5):GOSUB 20005:P(5)=M:PRINT T$;M
270 LOCATE 6,13: T$="CRISTAL   ":P=12:GOSUB 20000:E(6)=C: R=D(BARRIO,6):GOSUB 20005:P(6)=M:PRINT T$;M
280 LOCATE 6,14: T$="PEGAMENTO ":P=10:GOSUB 20000:E(7)=C: R=D(BARRIO,7):GOSUB 20005:P(7)=M:PRINT T$;M
300 K$=INKEY$:IFK$<>""THENONINSTR("vViIcC",K$)GOTO405,405,505,505,605,605
305 GOTO300
310 CLS:LOCATE 1,2:PRINT "V";VIDA
311 LOCATE 7,2:PRINT "D";DAY
312 LOCATE 11,2:PRINT USING"$ #########";DINERO
313 LOCATE 24,2:PRINT USING"% ######";DEUDA
314 LOCATE 6,7: M=P(0):PRINT "ANFETAMINA";M
320 LOCATE 6,8: M=P(1):PRINT "HACHIS    "M
330 LOCATE 6,9: M=P(2):PRINT "MARIHUANA ";M
340 LOCATE 6,10: M=P(3):PRINT "COCAINA   ";M
350 LOCATE 6,11: M=P(4):PRINT "MDMA      ";M
360 LOCATE 6,12: M=P(5):PRINT "KRIPTONITA";M
370 LOCATE 6,13: M=P(6):PRINT "CRISTAL   ";M
380 LOCATE 6,14: M=P(7):PRINT "PEGAMENTO ";M
390 K$=INKEY$:IFK$<>""THENONINSTR("vViIcC",K$)GOTO405,405,505,505,605,605
395 GOTO390
400 REM ***** SELECCION DE BARRIO PARA VIAJAR *****
405 CLS:X=8:Y=1:T$="BARRIOS":GOSUB 9100:FORB=0TO4:LOCATE6,B+8:PRINT B$(B):NEXT
410 BARRIO=0
470 PUT SPRITE 0,(32,BARRIO*8+64),8,0:FORT=0TO200:NEXT
471 ONSTICK(0)GOTO473,474,474,474,475:IFSTRIG(0)GOTO480ELSE471
473 BARRIO=BARRIO-1:IF BARRIO<0THENBARRIO=0
474 GOTO 470
475 BARRIO=BARRIO+1:IF BARRIO>4THENBARRIO=4
476 GOTO 470
480 FORT=0TO INT(RND(-TIME)*100):C=INT(RND(-TIME)*10):IF C>5 GOTO 481 ELSE 499
481 CLS: PUT SPRITE 0,(0,0),0,0:PROFO=0:FORC=0TO7:C(C)=0:NEXTC
482 X=8:Y=8:T$="OCURRIO UN EVENTO":GOSUB 9100:FORT=0to1500:NEXTT
483 FORT=0TO INT(RND(-TIME)*100):R=INT(RND(1)*10)
484 IF R>7 GOTO485 ELSE IF R>4 GOTO 490 ELSE IF R=>0GOTO495
485 CLS:X=8:Y=8:T$="POLICIA!":GOSUB 9100:FORT=0to1500:NEXTT: GOTO 9000
490 CLS:X=8:Y=8:T$="JONKIE!":GOSUB 9100:FORT=0to1500:NEXTT: GOTO 9000
495 CLS:X=8:Y=8:T$="BONUS!":GOSUB 9100:FORT=0to1500:NEXTT: GOTO 9000
499 GOTO 9000
500 REM ***** VISIONADO DE INVENTARIO Y DEUDA *****
505 CLS:X=8:Y=1:T$="INVENTARIO":GOSUB 9100
510 FOR A=0TO8:INV=INV+I(A):NEXTA:MO=100-(MOCHILA-INV)
515 LOCATE 6,4:PRINT "MOCHILA:";MO"/100"
520 LOCATE 6,6:PRINT "ANFETAMINA:  ";I(0)
530 LOCATE 6,7:PRINT "HACHIS:      ";I(1)
540 LOCATE 6,8:PRINT "MARIHUANA:   ";I(2)
550 LOCATE 6,9:PRINT "COCAINA:     ";I(3)
560 LOCATE 6,10:PRINT "MDMA:        ";I(4)
565 LOCATE 6,11:PRINT "KRIPTONITA:  ";I(5)
570 LOCATE 6,12:PRINT "CRISTAL:     ";I(6)
575 LOCATE 6,13:PRINT "PEGAMENTO:   ";I(7)
580 LOCATE 6,14:PRINT "MUNICION:    ";I(8)
590 K$=INKEY$:IFK$="m"ORK$="M" GOTO 310 ELSE 590
600 REM ***** COMPRA VENTA MADAFAKAS *****
605 CLS:X=8:Y=1:T$="TRAPICHEAR":GOSUB 9100
610 DROJA=0
611 LOCATE 14,5:PRINT "UNI."
612 LOCATE 20,5:PRINT "STOCK"
620 LOCATE 2,6:PRINT "ANFETAMINA: ";E(0): LOCATE 21,6:PRINT I(0)
630 LOCATE 2,7:PRINT "HACHIS:     ";E(1): LOCATE 21,7:PRINT I(1)
640 LOCATE 2,8:PRINT "MARIHUANA:  ";E(2): LOCATE 21,8:PRINT I(2)
650 LOCATE 2,9:PRINT "COCAINA:    ";E(3): LOCATE 21,9:PRINT I(3)
660 LOCATE 2,10:PRINT "MDMA:       ";E(4): LOCATE 21,10:PRINT I(4)
665 LOCATE 2,11:PRINT "KRIPTONITA: ";E(5): LOCATE 21,11:PRINT I(5)
666 LOCATE 2,12:PRINT "CRISTAL:    ";E(6): LOCATE 21,12:PRINT I(6)
667 LOCATE 2,13:PRINT "PEGAMENTO:  ";E(7): LOCATE 21,13:PRINT I(7)
668 LOCATE 2,16:PRINT "PROFORMA:   ";PROFO
670 PUT SPRITE 0,(5,DROJA*8 + 48),8,0:FORT=0TO200:NEXT
671 ONSTICK(0)GOTO674,,676,,681,,683,,:IF STRIG(0) GOTO 690
673 GOTO 671
674 DROJA=DROJA-1:IF DROJA<0THENDROJA=0
675 GOTO 670
676 IF E(DROJA)=0 THEN 671 ELSE 677
677 IF PROFO+P(DROJA)>DINERO THEN 671 ELSE 678
678 A=I(DROJA):I(DROJA)=(A+1):C(DROJA)=C(DROJA)+1:A=E(DROJA):E(DROJA)=(A-1): PROFO=(C(0)*P(0))+(C(1)*P(1))+(C(2)*P(2))+(C(3)*P(3))+(C(4)*P(4))+(C(5)*P(5))+(C(6)*P(6))+(C(7)*P(7)):P=PROFO-P(DROJA)
679 REM 
680 GOTO 620
690 DINERO=DINERO-PROFO:CLS:PUT SPRITE 0,(0,0),0,0: GOTO 310
681 DROJA=DROJA+1:IF DROJA>7THENDROJA=7
682 GOTO 670
683 IFI(DROJA)=0 THEN 670 ELSE 684
684 A=I(DROJA):I(DROJA)=(A-1):C(DROJA)=C(DROJA)-1:A=E(DROJA):E(DROJA)=(A+1): PROFO=(C(0)*P(0))+(C(1)*P(1))+(C(2)*P(2))+(C(3)*P(3))+(C(4)*P(4))+(C(5)*P(5))+(C(6)*P(6))+(C(7)*P(7))
685 GOTO 620
1050 REM **** PLACE HOLDER DE COSAS INTERESANTES (SI TE LO CURRAS, SI NO, CACAFUTI)
1060 GOTO 1060
8999 REM **** LA SUBRUTINA DEL SPACIO ****
9000 PUT SPRITE 0,(0,0),0,0
9010 GOTO 190
9099 REM ***** SUBRUTINA PRINT LETRAS GRANDES *****
9100 FORI=1TOLEN(T$):J=ASC(MID$(T$,I,1))
9101 IFJ<>32THENLOCATEX+I-1,Y:PRINTCHR$(J+64):LOCATEX+I-1,Y+1:PRINTCHR$(J+96)
9102 NEXT:RETURN
9501 REM ***** DOPE WARS EMESEQUIS ******
9502 REM ***** INICIAMOS PANTALLA, VARIABLES, ARRAY Y GRAFICOS *****
9503 DEFINTA-Z:SCREEN1:WIDTH32:KEYOFF:COLOR15,1,1:SCREEN2,0,0:BLOAD"GFX.BIN",S:POKE&HFCAF,1:KEY5,"run 9513"+CHR$(13): REM BLOAD"SFX.BIN",R
9504 IFINKEY$=""GOTO9504
9513 C=0: BARRIO=0: VIDA=100: DAY=0: MOCHILA=100: DINERO=3000: DEUDA=3000:DIM B$(4),D(4,7),E(7),I(8),P(7),C(7)
9516 RESTORE 10000:FOR B=0TO4:READ B$(B):FOR A=0TO7:READ D(B,A): NEXT A: NEXT B
9517 REM ***** EMPIEZA LA MOVIDA *****
9518 SEED=TIME:CLS
9520 REM **** LOOP PRINCIPAL ****
9530 GOTO 190
9999 REM **** PRECIOS POR BARRIO (X) Y DROJA (Y) ****
10000 DATA "LUTXANA",10,50,100,300,600,1500,2000,2100
10001 DATA "LA IBERIA",12,45,80,250,750,3000,3200,3300
10002 DATA "OTXARKOAGA",8,40,60,350,950,1900,2000,2100
10003 DATA "BARRIKA",10,55,40,320,400,1200,1500,2000
10004 DATA "GALLUFA",9,25,30,700,480,2200,2300,2500
19999 REM **** PORCENTAJE SEGUN PRECIO QUE USAMOS TAMBIEN PARA CANTIDAD DISPONIBLE****
20000 C=INT(RND(-SEED)*P):
20001 RETURN
20004 REM **** P ES EL PORCENTAJE, U ES EL RANDOM PARA EL NEGATIVO Y M EL RESULTADO (SUMANDO O RESTANDO ALEATORIAMENTE) ****
20005 FORT=0TO INT(RND(-TIME)*100):NEXTT:P=(R/100)*C:U=INT(RND(1)*2):IF U=1THEN GOTO 20006 ELSE M=INT(P+R):RETURN
20006 M=INT(R-P)
20007 RETURN